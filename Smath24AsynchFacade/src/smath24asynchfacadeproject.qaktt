System smath24asynchfacade 
//mqttBroker "broker.hivemq.com" : 1883 eventTopic "servicemathouttopic" 
  
//Messages for the application  
Request dofibo   : dofibo(N) "to service"
Reply   fibodone : fibodone( CALLER,N,RESULT,ELABTIME ) for dofibo 
 
  
//Messages for the facademock         
Dispatch smathinfo : smathinfo( INFO )    
Dispatch usercmd   : usercmd( CMD )
   
Context ctxsmathfacade ip [host="localhost" port=8033]  
  
 
/*
 * SERVICE 
 */
QActor smathasynchfacade  context ctxsmathfacade {
	 
	State init initial { 
		println("$name  STARTS ") color blue		
	}	
	Transition t0 whenRequest dofibo -> work
	 
	State work{
		[#val CurMsg = currentMsg #]
		 printCurrentMessage 
         updateResource [# "$currentMsg" #]
	     delegateCurrentMsgTo actionexec
	}  
	Transition t0   whenRequest dofibo -> work

 }

QActor actionexec  context ctxsmathfacade withobj math using "MathUtils.create()" dynamicOnly{ 
	[# var CurRequestTime = 0L; var SOUT="todo" #]
	State init initial { 
		//println("$name | STARTS ")  color magenta
    }	  
    Transition t0 whenRequest dofibo -> work
	 
	State work{
		printCurrentMessage color magenta
		onMsg( dofibo : dofibo( N ) ){
			[#   
				var ReqArg  = payloadArg(0) 
 				var Sender  = currentMsg.msgSender() 
			#]  
			//println("$name | work N=$N ${sysUtil.curThread()}") color blue
			[# 
			   val T0 = getCurrentTime()  
 			   var F  = math.fibo( ReqArg.toInt() )  
 			   val TF = getDuration(T0)   
    		   SOUT   = "fibovalue($ReqArg , $F,  $TF)" 
			#]
 			println("$SOUT") color magenta 
			//forward display -m out : out($SOUT)
			updateResource [# "$SOUT" #]
			replyTo dofibo with fibodone : fibodone($Sender, $ReqArg, $F,  $TF)
 			//CALLER,N,RESULT,ELABTIME
			terminate 0 
 		} 
	} 
 }
     
QActor facademock context ctxsmathfacade withobj applutil using "DisplayObj.create()"{
	[# var N = 0  
 	#] 
	State s0 initial { 
		println("$name | STARTS ")  color blue
		delay 1000 //give time to start facadeviewer ...
//		observeResource smathasynchfacade msgid smathinfo
//		delegate smathinfo to facadeviewer //NO la qakinfrastr invia a facademock
    }   
    Goto getusercmd
    
    State getusercmd {
    	[# applutil.write("OPEN INPUT") #]	
     	[#  N =  applutil.waitUserCmd( "Nfibo>>>" ) #]    
         autodispatch usercmd : usercmd($N)
    }
     Transition t0 
        whenMsg usercmd and [# N>0  #]  -> handleusercmd
        whenMsg usercmd and [# N==0 #]  -> endofwork
    
    //INPUT deve essere scorrelato dalla gestione delle info
    State handleusercmd{
    	//printCurrentMessage color blue
    	onMsg( usercmd : usercmd(CMD)){
    		println("$name usercmd:${payloadArg(0)}") color blue
    		request  smathasynchfacade -m  dofibo : dofibo($N)  //potrebbe metterci molto tempo ...
    	}
    }      
    Transition t0 
    	whenReply fibodone -> showResult
    
    
    State showResult {
    	printCurrentMessage color blue 
    	[# applutil.write("$currentMsg") #]
    }	
    Goto getusercmd  //evito di inviare altro comando prima avere ricevuto risposta
    
    State endofwork{
    	//println("$name | SORRY: input not working (... in gradle)  ")  color red
    	[# applutil.write("$name | SORRY: input not working (... in gradle) ") #]
    }
}  

QActor facadeviewer context ctxsmathfacade withobj applutil using "DisplayObj.create()"{	
	State s0 initial { 
		println("$name | STARTS ")  color blue
		observeResource smathasynchfacade msgid smathinfo
    }   
    Transition t0 
    	whenMsg smathinfo  -> showinfo   //a causa mia o di altri
     
    State showinfo {  //entra qui se esiste facade web
    	printCurrentMessage color blue
    	[# applutil.write("$currentMsg") #]
    }
    Transition t0 
    	whenMsg smathinfo  -> showinfo
} 

Facade facadesmathasynch port 8088 usingactor smathasynchfacade inctx ctxsmathfacade  
                   appl "unibo.servicefacade24.Servicefacade24Application" 
                   UserLibs -f "servicefacade24-1.0" -f "servicefacade24-1.0-plain"
	
