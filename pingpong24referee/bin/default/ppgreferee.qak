System ppgreferee 
Dispatch ballview : ball(N)  "info  osservata | payload INDICATIVO"

Request info : info(X)
Reply   obsinfo : obsinfo(X) for info
 
Request obsconnect : obsconnect(X)
Reply   obsconnected : obsconnected(X) for obsconnect
 
Event startgame : startgame(X)
Event stopgame  : stopgame(X)
 
Dispatch endgame : endgame(X)
 
/*
 * Il referee richiede che siano attivi i players ping e pong
 * Solo dopo puÃ² rispondere alla richiesta obsconnect
 */
Context ctxreferee  ip [host="localhost" port=8023] 
Context ctxping     ip [host="127.0.0.1" port=8014] 
Context ctxpong     ip [host="127.0.0.1" port=8015]
   
ExternalQActor ping context ctxping   
ExternalQActor pong context ctxpong 

QActor referee context ctxreferee{ 
[# var N = 0 #]
	State s0 initial{ 
		println("$name STARTS ") color cyan
		observeResource ping msgid ballview
		observeResource pong msgid ballview   
	}
	Goto emitStart
		
	State emitStart{
		println("$name emits start ") color cyan
		emit startgame : startgame(oK)		
	}
	Transition t0 
		whenTime 1000          -> emitStart
		whenMsg ballview       -> handlegame
		whenRequest obsconnect -> handleConnect
	
    State handleConnect{
    	printCurrentMessage color red
    	replyTo obsconnect with obsconnected : obsconnected(done)
    }
	Transition t0 
		whenMsg ballview -> handlegame
	
	State handlegame{
		printCurrentMessage color blue
		[# N++ #]
		if [# N == 5 #]{  
			println("$name emits stop ") color cyan
			emit stopgame : stopgamegame(oK)
		}
		onMsg( ballview :  ball(0) ){
			autodispatch endgame : endgame(ok)
		}
	}
	Transition t0 whenMsg ballview       -> handlegame  
		          whenRequest obsconnect -> handleConnect
	              whenMsg endgame        -> checkRequest
	              //whenRequest info -> handleRequest

	
	State checkRequest{
		println("$name checkRequest ") color red
	} 
	Transition t0  
			whenTime 1000    -> endgame     //no testing
			whenRequest info -> handleRequest
	
	State handleRequest{  
		printCurrentMessage color red
		replyTo info with obsinfo : obsinfo($N)
	}
    Goto endgame
    
    State endgame{
		delay 500
		println("$name BYE ") color red
		[# System.exit(0) #]    	
    }	
 
}